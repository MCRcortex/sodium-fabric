plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'io.github.juuxel.loom-quiltflower' version '1.7.2'
}

apply from: "${rootProject.projectDir}/gradle/fabric.gradle"
apply from: "${rootProject.projectDir}/gradle/java.gradle"

archivesBaseName = project.archives_base_name
version = getVersion()
group = project.maven_group

loom {
    mixin {
        defaultRefmapName = "mixins.sodium.refmap.json"
    }

    if (findProperty("sodium.use_custom_jvm_args") != "false") {
        runs {
            it.configureEach {
                vmArgs("-Xmx5G", "-XX:+UseZGC")
            }
        }
    }

    accessWidenerPath.set file("src/main/resources/sodium.accesswidener")
}

configurations {
    includeModImplementation
    includeImplementation

    include.extendsFrom includeImplementation
    include.extendsFrom includeModImplementation

    implementation.extendsFrom includeImplementation
    modImplementation.extendsFrom includeModImplementation
}

repositories {
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"

        content {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API modules
    includeModImplementation fabricApi.module("fabric-api-base", project.fabric_version)
    includeModImplementation fabricApi.module("fabric-rendering-fluids-v1", project.fabric_version)
    includeModImplementation fabricApi.module("fabric-rendering-data-attachment-v1", project.fabric_version)
    includeModImplementation fabricApi.module("fabric-resource-loader-v0", project.fabric_version)

    includeImplementation 'io.github.douira:glsl-transformer:2.0.0-pre9'

    // Because we need the subprojects to be build as jars prior to their inclusion, we need to separate them to
    // order them correctly.
    implementation project(":components").subprojects
    include project(":components").subprojects

    if (findProperty("sodium.use_third_party_mods") != "false") {
        modRuntimeOnly "maven.modrinth:lazydfu:0.1.3"
       // modRuntimeOnly "maven.modrinth:starlight:1.1.1+1.19"
       // modRuntimeOnly "maven.modrinth:lithium:mc1.19.2-0.8.3"
       // modRuntimeOnly "maven.modrinth:ferrite-core:5.0.0-fabric"
    }
}

String getVersion() {
    String version = "${project.mod_version}+mc${project.minecraft_version}"

    if (findProperty("releaseType") != "stable") {
        version += "-unstable"
    }

    return version
}